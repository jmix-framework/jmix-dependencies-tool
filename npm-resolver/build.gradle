import io.jmix.dependencies.JmixNpmDependenciesPlugin

buildscript {
    repositories {
        gradlePluginPortal()
    }
    if (project.hasProperty('jmixPluginVersion')) {
        dependencies {
            classpath "io.jmix.gradle:jmix-gradle-plugin:${project.findProperty('jmixPluginVersion') ?: version}"
        }
    }
}

apply plugin: 'java'
if (project.hasProperty('jmixPluginVersion')) {
    apply plugin: 'io.jmix'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'com.vaadin'

    String jmixPluginVersion = project.findProperty('jmixPluginVersion')
    if (!jmixPluginVersion.startsWith("2.0")) {
        println("Disable optimizeBundle")
        vaadin {
            optimizeBundle = false
        }
    }
}

apply plugin: JmixNpmDependenciesPlugin



if (project.hasProperty('jmixVersion')) {
    jmix {
        bomVersion = project.findProperty('jmixVersion') ?: version
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://global.repo.jmix.io/repository/public'
    }
    if (project.hasProperty('jmixLicenseKey')) {
        String licenseKey = project.property('jmixLicenseKey')
        def premiumRepoUser = licenseKey.substring(0, licenseKey.indexOf('-'))
        def premiumRepoPass = licenseKey.substring(licenseKey.indexOf('-') + 1)
        maven {
            url = 'https://global.repo.jmix.io/repository/premium'
            credentials {
                username = premiumRepoUser
                password = premiumRepoPass
            }
        }
    }
}

dependencies {
}

test {
    useJUnitPlatform()
}