import io.jmix.dependencies.JmixDependenciesPlugin

buildscript {
    repositories {
        gradlePluginPortal()
    }
    if (project.hasProperty('jmixPluginVersion')) {
        dependencies {
            classpath "io.jmix.gradle:jmix-gradle-plugin:${project.findProperty('jmixPluginVersion') ?: version}"
            classpath "gradle.plugin.org.jetbrains.gradle.plugin.idea-ext:gradle-idea-ext:1.1.9"
        }
    }
}

apply plugin: 'java'
if (project.hasProperty('jmixPluginVersion')) {
    apply plugin: 'io.jmix'
    apply plugin: 'org.springframework.boot'
}

//todo
apply plugin: JmixDependenciesPlugin

if (project.hasProperty('jmixVersion')) {
    jmix {
        bomVersion = project.findProperty('jmixVersion') ?: version
    }
}

repositories {
    def publicRepoUrl = project.findProperty('jmixPublicRepository') ?: 'https://global.repo.jmix.io/repository/public'

    mavenCentral()
    maven {
        url publicRepoUrl
    }
    if (project.hasProperty('jmixLicenseKey')) {
        String licenseKey = project.property('jmixLicenseKey')
        def premiumRepoUrl = project.findProperty('jmixPremiumRepository') ?: 'https://global.repo.jmix.io/repository/premium'
        def premiumRepoUser = licenseKey.substring(0, licenseKey.indexOf('-'))
        def premiumRepoPass = licenseKey.substring(licenseKey.indexOf('-') + 1)
        maven {
            url = premiumRepoUrl
            credentials {
                username = premiumRepoUser
                password = premiumRepoPass
            }
        }
    }
}

dependencies {
}

test {
    useJUnitPlatform()
}